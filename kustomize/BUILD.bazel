load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@rules_kubebuilder//kustomize:kustomize-toolchain.bzl", "ARCH_MAP", "PLATFORM_MAP", "kustomize_toolchain")

filegroup(
    name = "srcs",
    srcs = glob([
        "*.bzl",
        "**/*.bzl",
    ]),
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "release-bin",
    srcs = glob(["bin/*"]),
    package_dir = "/bin",
)

pkg_tar(
    name = "release",
    srcs = [
        "BUILD.bazel",
        ":srcs",
    ],
    package_dir = "kustomize",
    visibility = ["//visibility:public"],
    deps = [":release-bin"],
)

toolchain_type(name = "toolchain_type")

[
    kustomize_toolchain(
        name = "kustomize_toolchain_%s_%s" % (os, arch),
        kustomize_bin = "@kustomize_%s_%s//:kustomize" % (os, arch),
    )
    for os in PLATFORM_MAP.keys()
    for arch in ARCH_MAP.keys()
]

[
    toolchain(
        name = "kustomize_%s_%s_toolchain" % (os, arch),
        exec_compatible_with = [
            "@platforms//os:%s" % os,
            "@platforms//cpu:%s" % arch,
        ],
        toolchain = ":kustomize_toolchain_%s_%s" % (os, arch),
        toolchain_type = ":toolchain_type",
        visibility = ["//visibility:public"],
    )
    for os in PLATFORM_MAP.keys()
    for arch in ARCH_MAP.keys()
]
